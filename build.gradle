plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.89'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id
base.archivesName = mod_id

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {runtimeClasspath.extendsFrom localRuntime}

repositories {
    mavenLocal()

    maven {
        name "CurseForge"
        url "https://www.cursemaven.com"
        content {includeGroup "curse.maven"}
    }
    maven {
        name "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {includeGroup "maven.modrinth"}
    }
    maven {
        name "Shadows"
        url "https://maven.shadowsoffire.dev/releases"
        content {includeGroup "dev.shadowsoffire"}
    }
    maven {
        name "JEI"
        url 'https://maven.blamejared.com/'
        content {includeGroup "mezz.jei"}
    }
    mavenCentral()
}

dependencies {
//    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
//    runtimeOnly "curse.maven:appleskin-248787:5864743"
    runtimeOnly "curse.maven:jade-324717:6155158"
//    runtimeOnly "curse.maven:modern-ui-352491:5977413"

    //with
//    runtimeOnly("maven.modrinth:curios:9.2.2+1.21.1")
//    runtimeOnly("maven.modrinth:forgified-fabric-api:0.107.0+2.0.23+1.21.1")
//    runtimeOnly("maven.modrinth:connector:2.0.0-beta.7+1.21.1")
//    runtimeOnly("maven.modrinth:connector-extras:1.12.1+1.21.1")

    jarJar(implementation("maven.modrinth:sword-blocking-mechanics:v21.1.1-1.21.1-NeoForge")) {
        version {
            prefer 'v21.1.1-1.21.1-NeoForge'
        }
    }
    jarJar(runtimeOnly("maven.modrinth:puzzles-lib:v21.1.27-1.21.1-NeoForge")) {
        version {
            prefer 'v21.1.27-1.21.1-NeoForge'
        }
    }

}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
    ]

    inputs.properties replaceProperties


    filesMatching('neoforge.mods.toml') {
        expand replaceProperties
        name 'META-INF/neoforge.mods.toml'
    }

    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets {
    main {
        resources {
            srcDir("src/generated/resources")
            srcDir(generateModMetadata)
        }
    }
}

neoForge.ideSyncTask generateModMetadata

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    interfaceInjectionData.from "src/main/templates/interface_injection.json"
    validateAccessTransformers = true
    accessTransformers.from 'src/main/resources/META-INF/accesstransformer.cfg'

    runs {
        client {
            client()
            gameDirectory = file("run/client")
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            gameDirectory = file("run/server")
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition", "-Dmixin.debug.export=true")
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}



publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
